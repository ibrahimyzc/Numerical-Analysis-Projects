int n=40;
mesh Th=square(n,n);

fespace Vh(Th,RT0);
fespace Ph(Th,P0);
fespace Phc(Th,P1);
Vh [u1,u2],[v1,v2];
Ph p,w; 
Phc c, c0 = exp(-100*((x-0.2)^2 +(y-0.5)^2));
Phc vel1, vel2;
real alpha = 1;
real dt=0.01, t=0;
func gd =1-x;
func f = 0;

func K = (9999.*((x>(0.4) & x<(0.6)) & (y>(0.4) & y<(0.6))))/(-10000)+1;

problem hwp1([u1,u2,p],[v1,v2,w],solver=LU,eps=-1.0e-6) =
   int2d(Th)( p*w*1e-15+ (1/K)*u1*v1 + (1/K)*u2*v2 - p*(dx(v1)+dy(v2)))
 + int2d(Th) ((alpha*p*w)+(dx(u1)+dy(u2))*w)
 + int1d(Th)( gd*(v1*N.x +v2*N.y))
 - int2d(Th)(f*w);

hwp1;
vel1=u1;
vel2=u2;

for (int m=0; m<1/dt ; m++) {
t += dt;
c=convect([vel1,vel2],-dt,c0);
c0=c; 
plot(c,cmm=" t="+t + ", min=" + c[].min + ", max=" + c[].max,wait=0);
}

plot(c,cmm=" t="+t + ", min=" + c[].min + ", max=" + c[].max,wait=true);
plot(c,fill=1,wait=1,ps="laRTc.eps",value=true);
plot([u1,u2],coef=0.1,wait=1,ps="lapRTuv.eps",value=true); 
plot(p,fill=1,wait=1,ps="laRTp.eps",value=true);